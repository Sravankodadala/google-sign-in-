# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

group("google_sign_in") {
  deps = [
    ":interfaces",
  ]

  if (is_ios) {
    deps += [ ":google_sign_in_lib" ]
  }
}

mojom("interfaces") {
  sources = [
    "mojom/google_sign_in.mojom",
  ]
}

if (is_ios) {
  shared_library("google_sign_in_lib") {
    output_name = "GoogleSignIn"

    sources = [
      "mojom/google_sign_in.mojom",
      "src/google_sign_in_impl.h",
      "src/google_sign_in_impl.mm",
      "sdk/GoogleSignIn.framework/Headers/GoogleSignIn.h",
      "//mojo/public/cpp/application/interface_factory.h",
    ]

    include_dirs = [
      "sdk/GoogleSignIn.framework/Headers/",
    ]

    libs = [
      "UIKit.framework",
      "GoogleSignIn.framework",
      "AddressBook.framework",
      "SafariServices.framework",
      "StoreKit.framework",
      "SystemConfiguration.framework",
      "Security.framework",
      "z",
    ]

    deps = [
      "//sky/services/dynamic:sdk_lib",
      ":interfaces",
    ]
    frameworks_dir = rebase_path("sdk", root_build_dir)
    ldflags = [
      "-F$frameworks_dir",
      "-ObjC",
    ]
  }
}
